#lang ivy1.7

module ring_topology(carrier) = {

    relation btw(X:carrier,Y:carrier, Z:carrier) # Y is on the acyclic path from X to Z

    # Axiom defining the btw relation - note it's not reflexive
    # not needed: axiom btw(X,Y,Z) -> X ~= Y & X ~= Z & Y ~= Z  # anti-reflexive
    axiom btw(W,X,Y) & btw(W,Y,Z) -> btw(W,X,Z)  # transitive
    axiom btw(W,X,Y) -> ~btw(W,Y,X)  # acyclic
    axiom btw(W,X,Y) | btw(W,Y,X) | W=X | W=Y | X=Y  # total
    axiom btw(X,Y,Z) -> btw(Y,Z,X)  # cyclic permutations

    action get_next(x:carrier) returns (y:carrier) = {
        assume x ~= y & ((Z ~= x & Z ~= y) -> btw(x,y,Z))
    }

    action get_prev(y:carrier) returns (x:carrier) = {
        assume y ~= x & ((Z ~= y & Z ~= x) -> btw(y,x,Z))
    }

}

type node
type value
type pcstate

instantiate ring : ring_topology(node)

function x(N:node) : value
function y(N:node) : value

individual zero: value
individual one: value
axiom zero ~= one

function pc(N:node) : pcstate
individual a: pcstate
individual b: pcstate
individual done: pcstate
axiom (a ~= b) & (a ~= done) & (b ~= done)
#axiom forall P:pcstate. (P = a) | (P = b) | (P = done)

after init {
	x(N) := zero;
	y(N) := zero;
	pc(N) := a;
}

action step_a(self:node) = {
	require (pc(self) = a);
	x(self) := one;
	pc(self) := b;
}

action step_b(self:node) = {
	require (pc(self) = b);
	y(self) := x(ring.get_prev(self));
	pc(self) := done;
}

export step_a
export step_b

invariant [safety] (forall S:node. pc(S) = done) -> (exists I:node. y(I) = one)

invariant [manual_1] forall I:node. (x(I) = zero) | (x(I) = one)
invariant [manual_2] forall I:node. (y(I) = zero) | (y(I) = one)
invariant [manual_3] forall I:node. (pc(I) = a) | (pc(I) = b) | (pc(I) = done)
invariant [manual_4] forall I:node. pc(I) = done -> x(I) = one
invariant [manual_5] (exists I:node. pc(I) ~= done) | (exists I:node. y(I) = one)

invariant [manual_ic3po6]	(forall N0 . ((pc(N0) = a) -> ~(x(N0) = one)))
invariant [manual_ic3po7]	(forall N0 . ((x(N0) = zero) -> (pc(N0) = a)))
