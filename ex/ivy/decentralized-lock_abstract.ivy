#lang ivy1.7

# Source: https://github.com/sat-group/cfg-enum/blob/master/examples/decentralized-lock.ivy

type node
type time

relation le(X: time, Y: time)
axiom forall X : time . le(X, X)
axiom forall X : time . forall Y : time . le(X, Y) & le(Y, Z) -> le(X, Z)
axiom forall X : time . forall Y : time . (le(X, Y) & le(Y, X)) -> X = Y
axiom forall X : time . forall Y : time . le(X, Y) | le(Y, X)

relation has_lock(X: node)
relation msg(X: node, Y: node, E: time)
relation gt(T: time, X: node)

individual first: node
individual zero: time
axiom le(zero, X)

after init {
	has_lock(X) := (X = first);
	msg(X,Y,T) := false;
	gt(T, X) := (T ~= zero);
}

action take_lock(x: node, y: node, t: time) = {
    require msg(x, y, t);
    require gt(t, y);
    has_lock(y) := true;
    gt(T, y) := true if ~le(T, t) else false;
}

action release_lock(x: node, y : node, t: time) = {
    require has_lock(x);
    require gt(t, x);
    has_lock(x) := false;
    msg(x, y, t) := true;
}

export take_lock
export release_lock

# safety property

invariant ~(X ~= Y & has_lock(X) & has_lock(Y))

#invariant [manual_1] ~(X ~= Y & ( has_lock(X) | (msg(X1, X, T) & gt(T, X)) ) & ( has_lock(Y) | (msg(Y1, Y, S) & gt(S, Y)) ))
#invariant [manual_2] ~(has_lock(X) & (msg(X1, X, T) & gt(T, X)))
#invariant [manual_3] ~(S ~= T & msg(X1, Y, S) & msg(X2, Y, T) & gt(T, Y) & gt(S, Y))
#invariant [manual_ic3po4]	(forall N0, T0, T1 . ((le(T0, T1) & gt(T0, N0)) -> gt(T1, N0)))
#invariant [manual_ic3po5]	(forall N0 . ~gt(zero, N0))

